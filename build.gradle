plugins {
    id 'java'
}

group 'com.reandroid.apkeditor'
version '1.4.7'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    implementation files("${rootProject.projectDir}/libs/ARSCLib.jar")
    implementation files("${rootProject.projectDir}/libs/smali.jar")
    implementation files("${rootProject.projectDir}/libs/JCommand.jar")
}

processResources {
    filesMatching('apkeditor.properties') {
        expand('version': version)
    }
}

jar {
    exclude('com/reandroid/test/**')
    duplicatesStrategy = DuplicatesStrategy.FAIL
}

tasks.register('fatJar', Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveClassifier = ''
    
    manifest {
        attributes(
            'Main-Class': 'com.reandroid.apkeditor.Main',
            'Implementation-Title': 'APKEditor',
            'Implementation-Version': version
        )
    }
    
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}
